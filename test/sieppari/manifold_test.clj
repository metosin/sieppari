(ns sieppari.manifold-test
  (:require [clojure.test :refer :all]
            [testit.core :refer :all]
            [sieppari.core :as sc]
            [manifold.deferred :as d]))

(defn make-logging-interceptor [log name]
  {:name name
   :enter (fn [ctx] (swap! log conj [:enter name]) ctx)
   :leave (fn [ctx] (swap! log conj [:leave name]) ctx)
   :error (fn [ctx] (swap! log conj [:error name]) ctx)})

(defn make-async-logging-interceptor [log name]
  {:name name
   :enter (fn [ctx] (swap! log conj [:enter name]) (d/success-deferred ctx))
   :leave (fn [ctx] (swap! log conj [:leave name]) (d/success-deferred ctx))
   :error (fn [ctx] (swap! log conj [:error name]) (d/success-deferred ctx))})

(defn make-logging-handler [log]
  (fn [request]
    (swap! log conj [:handler])
    request))

(defn make-async-logging-handler [log]
  (fn [request]
    (swap! log conj [:handler])
    (d/success-deferred request)))

(def request {:foo "bar"})
(def error (ex-info "oh no" {}))

(defn fail! [& _]
  (throw (ex-info "Should never be called" {})))

(deftest setup-sync-test-test
  (let [log (atom [])
        response (-> [(make-logging-interceptor log :a)
                      (make-logging-interceptor log :b)
                      (make-logging-interceptor log :c)
                      (make-logging-handler log)]
                     (sc/execute request))]
    (fact
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:leave :c]
               [:leave :b]
               [:leave :a]])
    (fact
      response = request)))

(deftest setup-async-test-test
  (let [log (atom [])
        response (-> [(make-async-logging-interceptor log :a)
                      (make-async-logging-interceptor log :b)
                      (make-async-logging-interceptor log :c)
                      (make-async-logging-handler log)]
                     (sc/execute request))]
    (fact
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:leave :c]
               [:leave :b]
               [:leave :a]])
    (fact
      response = request)))

(deftest async-b-sync-execute-test
  (let [log (atom [])
        response (-> [(make-logging-interceptor log :a)
                      (make-async-logging-interceptor log :b)
                      (make-logging-interceptor log :c)
                      (make-logging-handler log)]
                     (sc/execute request))]
    (fact
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:leave :c]
               [:leave :b]
               [:leave :a]])
    (fact
      response => request)))

(deftest async-handler-test
  (let [log (atom [])
        response (-> [(make-logging-interceptor log :a)
                      (make-logging-interceptor log :b)
                      (make-logging-interceptor log :c)
                      (make-async-logging-handler log)]
                     (sc/execute request))]
    (fact
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:leave :c]
               [:leave :b]
               [:leave :a]])
    (fact
      response => request)))

(deftest async-stack-sync-execute-test
  (let [log (atom [])
        response (-> [(make-logging-interceptor log :a)
                      (make-logging-interceptor log :b)
                      (make-logging-interceptor log :c)
                      (make-async-logging-handler log)]
                     (sc/execute request))]
    (fact
      response => request)
    (fact {:timeout 100}
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:leave :c]
               [:leave :b]
               [:leave :a]])))

(deftest async-stack-async-execute-test
  (let [log (atom [])
        response-p (promise)]
    (-> [(make-logging-interceptor log :a)
         (make-logging-interceptor log :b)
         (make-logging-interceptor log :c)
         (make-async-logging-handler log)]
        (sc/execute request (partial deliver response-p) fail!))
    (fact {:timeout 100}
      @response-p => request)
    (fact {:timeout 100}
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:leave :c]
               [:leave :b]
               [:leave :a]])))

(deftest async-execute-with-error-test
  (let [log (atom [])
        response-p (promise)]
    (-> [(make-logging-interceptor log :a)
         (make-logging-interceptor log :b)
         (make-logging-interceptor log :c)
         (fn [_]
           (swap! log conj [:handler])
           (throw error))]
        (sc/execute request fail! (partial deliver response-p)))
    (fact {:timeout 100}
      @response-p => error)
    (fact {:timeout 100}
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:error :c]
               [:error :b]
               [:error :a]])))

(deftest async-failing-handler-test
  (let [log (atom [])]
    (fact
      (-> [(make-logging-interceptor log :a)
           (make-logging-interceptor log :b)
           (make-logging-interceptor log :c)
           (fn [_]
             (swap! log conj [:handler])
             (d/success-deferred error))]
          (sc/execute request))
      =throws=> error)
    (fact
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:error :c]
               [:error :b]
               [:error :a]])))

(deftest async-failing-handler-b-fixes-test
  (let [log (atom [])]
    (fact
      (-> [(make-logging-interceptor log :a)
           (assoc (make-async-logging-interceptor log :b)
             :error (fn [ctx]
                      (swap! log conj [:error :b])
                      (d/success-deferred
                        (-> ctx
                            (assoc :error nil)
                            (assoc :response :fixed-by-b)))))
           (make-logging-interceptor log :c)
           (fn [_]
             (swap! log conj [:handler])
             (d/success-deferred error))]
          (sc/execute request))
      => :fixed-by-b)
    (fact
      @log => [[:enter :a]
               [:enter :b]
               [:enter :c]
               [:handler]
               [:error :c]
               [:error :b]
               [:leave :a]])))
